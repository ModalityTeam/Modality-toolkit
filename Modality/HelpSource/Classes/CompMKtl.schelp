TITLE:: CompMKtl
summary:: create composite MKtls
categories:: Modality
related:: Classes/MKtl, Overviews/Modality

DESCRIPTION::
CompMKtl allows combining MKtls into a single MKtl which contains the elements of all these MKtls. For playing interfaces consisting of technically separate devices, it may semantically make sense to merge them into a single MKtl, so one
create groups of elements that belong together.
CompMKtl is only a first pass at this and incomplete,
so this file also contains tests.


First code examples:

code::
// make some component MKtls, then combine them
MKtl(\nk2a, "*nanokontrol2");
MKtl(\nk2b, "*nanokontrol2");
MKtl(\gpd, "*impact*");  // a gamepad
MKtl(\gyro, "gyrosc_ga");  // phone app with gyro and accel

// combine two identical devices into one:
a = CompMKtl(\combi, [\nk2a, \nk2b]);
// the MKtls become names in the elementGroup
a.mktlDict;
a.mktlNames;
a.elementGroup;
// access nanoktl at 0, sl 5
a.elAt(0, \sl, 5);
a.elAt(1, \sl, 6);

// lookup by mktl name :
a.elAt(\nk2b, \sl, 7);

// combine two different devices:
b = CompMKtl(\ng, [\nk2a, \gpd]);
b.elAt(0, \play);
b.elAt(1, \joy);

b.elAt(\nk2a, \play);
b.elAt(\gpd, \joy);


// two different ones
c = CompMKtl(\gpgy, [\gpd, \gyro]);
c.elementGroup;
c.elAt(1);
c.elAt(0);

c.elAt(\gyro, \accel, \x);


// and make one with 3 devices
d = CompMKtl(\ngg, [\nk2a, \gpd, \gyro]);
d.elementGroup;
d.elAt(0, \rew);
d.elAt(1, \bt, 0);
d.elAt(2, \gyro, \pitch);

d.elAt(\nk2a, \rew);
d.elAt(\gyro, \gyro, \pitch);
d.elAt(\gpd, \bt, 0);



// test flattening elementGroups:

// two nanokontrols: cannot flatten into a single ElementGroup,
// because the two groups have the same element/group-names:
a.elementGroup.canFlatten;

// nanokontrol and gamepad: no luck, both have \bt elements
b.elementGroup.canFlatten;

// gamepad and gyro - yes!
c.elementGroup.canFlatten;
c.elementGroup;
// so do it:
c.flattenElementGroup;
c.elementGroup; // now elementGroup has the groups from

// lookup by index
c.elAt(1);
// and by name
c.elAt(\joy);
c.elAt(\accel, \x);

::

CLASSMETHODS::

METHOD:: all
dict for all CompMKtls

private:: initClass

METHOD:: new
make a new MKtl with a name, and the names of existing MKtls
argument:: name
argument:: mktlNames


INSTANCEMETHODS::

METHOD:: name
This CompMKtl's name and also its key in .all

METHOD:: mktlNames
The names of the composed MKtls

METHOD:: mktlDict
A dict with the MKtls

METHOD:: elementGroup
The elementGroup containing all elements

METHOD:: elementAt
look up element as in MKtl

METHOD:: elAt
like elementAt, but returns nil when find fails.

METHOD:: flattenElementGroup
flattens the elements in the top level group if possible.

private:: init
