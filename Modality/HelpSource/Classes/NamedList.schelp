
TITLE:: NamedList
summary:: an ordered list of items which can be accessed by index or name.
categories:: Collections, Classes/List
related:: Classes/Collection, Classes/List, Classes/Dictionary

DESCRIPTION::
Like a List, a NamedList has an array of items which can be accessed by index. Like a dictionary, its items can also be accessed by names. Items can be added and removed and replaced by name. It also supports pseudo-object style like a dict.

Note::
NamedList is designed for semantic clarity and for fast lookup:
code::put, at:: methods are reasonably fast, direct lookup with code::namedlist.dict[key]:: or code::namedlist.array[index]:: is nearly as fast as with Array and Event.

code::add, remove, removeAt:: methods are slower than with arrays or dicts, due to the overhead of keeping array, names and dict in sync. This design assumes that lookup will happen some orders of magnitude more often than add/remove.
::

code::
// make a new NamedNist
n = NamedList([ \a, 4, \b, 5, \c, 6 ]);

//  access list-like by index, get and set
n[0];
n.at([0, 2]); 	// at supports multiple values by index
n[0] = 12;
n.put(2, 13);

//  access dict-like by name, get and set
n[\a];
n.at(\a);
n.at([\b, \c]);	// multiple values
n.at([\a, 2]); 	// can also mix indices and keys

n[\a] = 123;
n.put(\d, 7); 	// new names get added to the end

// access by name as pseudo-method (when n.know == true)
n.a;
n.eee;
n.eee = 555; 	// adding also works by pseudo-method

// internals, can be used for faster lookup
n.dict;
n.array.postcs;
n.names.postcs;
::

CLASSMETHODS::

METHOD:: new, fromPairs
create new NamedList from array of pairs

code:: n = NamedList([ \a, 4, \b, 5, \c, 6 ]); ::

METHOD:: fromDict
create new NamedList from dict and optional ordered names or sortFunc.
code::
n = NamedList.fromDict((a: 1, b: 3, c: 5), [\c, \a, \b]);
n = NamedList.fromDict((a: 1, c: 5, b: 3), sortFunc: { |a, b| a > b });
::

METHOD:: newUsing
create new NamedList from array of values and names
code:: NamedList.newUsing([1, 3, 5], [\a, \b, \c]); ::

METHOD:: fromAssocs
create new NamedList from array of associations:
code:: n = NamedList.fromAssocs([\a -> 1, \b -> 3, \c -> 5]); ::

INSTANCEMETHODS::

METHOD:: names
the ordered list of names

code:: n.names.postcs; ::

METHOD:: dict
the unordered dict of items - for fast access by key

code:: n.dict.postcs; ::

METHOD:: know
flag whether to use pseudomethods or not
code::
n.know;
n.know = false;
::

private:: init, printOn, doesNotUnderstand, storeArgs, dump

METHOD:: at
put by key (name), index or collection of keys/indices
code::
n[0];
n.at(0);
n.at([0, 2]);

//  access dict-like by name, get and set
n[\a];
n.at(\a);
n.at([\b, \c]);	// multiple values
n.at([\a, 2]); 	// can also mix indices and keys
n.a;			// if n.know == true, can access by name as pseudo-method
::

METHOD:: put
put by key (name) or index
code::
n[0] = 11;
n.put(2, 33);
n.put(\a, 111);
n.put(\c, 333);
n[\x] = 999;
::

Method:: add
add by name; if name exists, replaces item at name, else adds item to end
code:: n.add(\aaa, \xyz); ::

Method:: remove
remove item from dict, array, and remove corresponding name.
code:: n.remove(\xyz); ::

Method:: removeAt
remove item at key, index or list of keys and indices.
code::
n.removeAt(\a);
n;
n.removeAt([\b, \c]);
n;
::


EXAMPLES::

code::
// make a new NamedNist
n = NamedList([ \a, 4, \b, 5, \c, 6 ]);

// or more efficiently, but less readable
n = NamedList.newUsing([4, 5, 6], [\a, \b, \c]);


//  access list-like by index, get and set
n[0];
n.at(0);

n.at([0, 2]); 	// multiple values by index
n.at(4); 		// out of range = nil

n[0] = 12;
n.put(2, 13);
n.put(4, 13);  	// out of range put fails, like list

//  access dict-like by name, get and set
n[\a];
n.at(\a);
n.at([\b, \c]);	// multiple values
n.at([\a, 2]); 	// can also mix indices and keys

n.put(\a, 123);
n.put(\d, 7); 	// new names get added to the end
n[\d] = 77;

n.a;			// if n.know == true, can access by name as pseudo-method
n.eee;
n.eee = 555; 	// adding also works by pseudo-method

// internals, can be used for faster lookup
n.dict;
n.array.postcs;
n.names.postcs;
n.dump
n
n.remove(555);
n.remove(5);
n.removeAt(\a);
n.removeAt(\yo);

[1, 2, 3].remove(4);
n = NamedList([ \a, 4, \b, 5, \c, 6 ]);
n.removeAt([\a, \b, \yo]);
n.dump;

n.know			// flag whether to use pseudomethods. true by default

// conversions from other formats:
n = NamedList.fromPairs([a: 1, b: 3, c: 5]);
n = NamedList.fromAssocs([\a -> 1, \b -> 3, \c -> 5]);

// alphabetical order by default
n = NamedList.fromDict((a: 1, b: 3, c: 5));

// or specify order:
n = NamedList.fromDict((a: 1, b: 3, c: 5), [\c, \b, \a]);

[a: 1, b: 3, c: 5].asNamedList;
(a: 1, b: 3, c: 5).asNamedList;
[\a -> 1, \b -> 3, \c -> 5].asNamedList;
::